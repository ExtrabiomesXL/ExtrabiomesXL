evaluationDependsOn(':version')

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'signing'

group = "net.extrabiomes"
archivesBaseName = "extrabiomesxl_"+ project.mcversion

ext.apiATG = "./api/atg"
ext.apiForestry = "./api/forestry"
ext.apiNewDawn = "./api/newdawn"
ext.apiThaumcraft = "./api/thaumcraft"

minecraft {
    version = project.mcversion + "-" + project.forgeversion
    runDir = "run"
    
    replace '@VERSION@', project.version
}

sourceSets {
	main {
		java {
			srcDir project.apiATG
			srcDir project.apiForestry
			srcDir project.apiNewDawn
			srcDir project.apiThaumcraft
			exclude '**/foresty/api/arborculture/**'
			exclude '**/foresty/api/circuits/**'
			exclude '**/foresty/api/food/**'
			exclude '**/foresty/api/fuel/**'
			exclude '**/foresty/api/lepidopterology/**'
			exclude '**/foresty/api/mail/**'
			exclude '**/foresty/api/world/**'
			exclude '**/thaumcraft/api/damagesource/**'
			exclude '**/thaumcraft/api/entities/**'
			exclude '**/thaumcraft/api/nodes/**'
			exclude '**/thaumcraft/api/potions/**'
			exclude '**/thaumcraft/api/visnet/**'
			exclude '**/thaumcraft/api/wands/**'
		}
	}
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'

    	expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

jar {
	exclude 'forestry'
    exclude 'thaumcraft'
	exclude 'ttftcuts'
	exclude 'two'

	includeEmptyDirs = false
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource 
    appendix = 'deobf'
    classifier = 'src'
    exclude 'forestry/**'
    exclude 'thaumcraft/**'
    exclude 'ttftcuts/**'
    exclude 'two/**'
}

// create a deobfuscated jar for developers
task devJar(type: Jar) {
	from(sourceSets.main.output) {
		include '**'
	}

	exclude 'forestry'
    exclude 'thaumcraft'
	exclude 'ttftcuts'
	exclude 'two'

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	classifier = 'dev'
}

// create a source jar
task srcJar(type: Jar) {
	from(sourceSets.main.allSource) {
		include '**'
	}

	exclude 'forestry'
    exclude 'thaumcraft'
	exclude 'ttftcuts'
	exclude 'two'

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	classifier = 'src'
}

// create an api jar for developers
task apiJar(type: Jar, dependsOn: 'classes') {
	from (sourceSets.main.output) {
		include 'extrabiomes/api/**'
	}
	extension = 'jar'
	classifier = 'api'
}

artifacts {
    archives apiJar
    archives srcJar
    archives devJar
}

signing {
    required { gradle.taskGraph.hasTask(":uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ExtrabiomesXL'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'The ExtrabiomesXL mod for Minecraft.'
                url 'http://extrabiomes.net/'

                scm {
                    connection 'scm:git:git@github.com:ExtrabiomesXL/ExtrabiomesXL.git'
                    developerConnection 'scm:git:git@github.com:ExtrabiomesXL/ExtrabiomesXL.git'
                    url 'http://github.com/ExtrabiomesXL/ExtrabiomesXL'
                }

                licenses {
                    license {
                        name 'Creative Commons Attribution-ShareAlike 3.0 Unported License'
                        url 'http://creativecommons.org/licenses/by-sa/3.0/deed.en_US'
                    }
                }
            }
        }
    }
}
